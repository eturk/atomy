use("core")
define = use("define")
use("data")
use("control-flow")
use("loop")
use("interpolation")
use("array")

use("dynamic")

io = use("io")
pairs = require("pairs")
pretty() = @pretty ||= require("pretty")

data(Handler(@callback, @rest = nil)):
  BaseHandler()

-- registered handlers/restarts
Handlers = dynamic(BaseHandler new)
Restarts = dynamic([])

catcher(x) = ."restart:#{define message-name(x)}"
message-name(x) = define message-name(x)
arguments(x) = define arguments(x)
block(x) = define block(x)

data(Restart(@name, @action))

Restart invoke(*args) :=
  throw(."restart:#{@name}", @action [*args])

data(Condition):
  Error(@backtrace):
    SimpleError(@value)
    ExceptionError(@exception)
    NoRestartError(@restart)
    PortError(@port):
      EndOfFile

  Warning(@backtrace):
    SimpleWarning(@value)

Condition name := class name

ExceptionError name := @exception class name

Condition message := inspect

SimpleError(s: String) message := s
SimpleError(v) message := pretty show(v)

SimpleWarning(s: String) message := s
SimpleWarning(v) message := pretty show(v)

ExceptionError message := @exception message

NoRestartError message := "unknown restart " + pretty show(@restart)

EndOfFile message := "unexpected end-of-file for " + pretty show(@port)


macro(~body bind: ~*handlers):
  names [a]:
    callback = `([~a]: ~a match: ~*handlers)
    `(let(Handlers = Handler new(~callback, ^Handlers)):
        ~body rescue:
          (e: StandardError) -> /error(e))


macro(with-restarts(~*restarts): ~*body):
  branches = pairs from(restarts)

  rs = branches collect [n, e]:
    `(Restart new(
        ~message-name(n)
        [~*arguments(n)] &~block(n) { ~e }))

  with =
    branches reduce(`{ { ~*body } rescue: (e: StandardError) -> /error(e) })
      [x, [name, _]]:
        `{ catch(~catcher(name)) ~x }

  `(let(Restarts = [~*rs] + ^Restarts) ~with)


restart(name, *args) := do:
  ^Restarts each [r]:
    when(r name == name):
      r invoke(*args)

  error(NoRestartError new(name))


BaseHandler signal(_) := nil
Handler(callback, rest) signal(c) :=
  let(Handlers = rest):
    callback [c]
    rest signal(c)

signal(c) := ^Handlers signal(c)

error(x) := do:
  e =
    x match:
      Exception ->
        ExceptionError new(x) tap [err]:
          err backtrace = x locations

      Error -> x

      _ -> SimpleError new(x)

  unless(e backtrace):
    e backtrace = Rubinius VM backtrace(1, true)

  signal(e)

  with-output-to(^(io ErrorPort)):
    ^Debugger new(e) run


warning(x) :=
  with-restarts(muffle-warning -> nil):
    w =
      x match:
        Warning -> x

        _ -> SimpleWarning new(x)

    w backtrace = Rubinius VM backtrace(1, true)

    signal(w)

    with-output-to(^(io ErrorPort)):
      puts(w name + ": " + w message)

    nil


data(DefaultDebugger(@error))

DefaultDebugger(e) show-banner := do:
  puts("An error occurred:")

  (msg . desc) = e message to-s split("\n")

  puts("    " + msg + " (" + e name + ")")

  desc each [l]:
    puts(l)

DefaultDebugger(e) show-restarts := do:
  puts("")
  puts("restarts:")
  ^Restarts each-with-index [r, i]:
    puts("  :" + i to-s + " -> " + r name to-s)

DefaultDebugger(e) show-backtrace :=
  puts(Atomy Backtrace backtrace(e backtrace) show)

DefaultDebugger(e) run := do:
  show-banner

  if(^Restarts empty?)
    then:
      puts("")
      puts("Backtrace:")
      show-backtrace
      exit(1)
    else:
      show-restarts

      print("!> ")
      ^Restarts [gets to-i] invoke

Debugger = dynamic(DefaultDebugger)
