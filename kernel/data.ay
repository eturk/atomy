use("core")
use("define")
use("control-flow")
use("init")

-- patterns defined by dsl use 'with', so make sure it's exported
export(use("patterns"))


define-class(nil, e: Atomy AST Constant) = `(~e = class:)
define-class(root, e: Atomy AST Constant) = `(~e = ~root class:)
define-class(root, `(~n { ~*cs })) =
  define-class(root, `((~n)() { ~*cs }))
define-class(root, x: `((~name)(~*as))) =
  define-class(root, `(~x {}))
define-class(root, `((~name)(~*as): ~*cs)) = do:
  attrs = []
  as each [a]:
    a through-quotes [x]:
      x match:
        `@~_ -> attrs << x

      x

  tmps = names(attrs size) collect [t]:
    init(Atomy AST Unquote, expression: t)

  pat = name
  attrs zip(tmps) [a, t]:
    pat =! `(~pat with(~a, ~t))

  cons = `((~name)(~*tmps))

  pat-def =
    `(pattern(~init(Atomy AST QuasiQuote, expression: cons)):
        pattern(~init(Atomy AST QuasiQuote, expression: pat)))

  parent = root or 'Object

  names [copy]:
    `(do:
        ~name = ~parent class:
          attr-accessor(
            ~*(attrs collect [a]:
                `.~(a receiver)))

          initialize(~*as) := nil

          copy := do:
            ~copy = super

            ~*(attrs collect [a]:
                `(~copy ~(a receiver) = ~a copy))

            ~copy

          inspect := do:
            comma = ", "
            result = ~(name name to-s) + "("

            recursed = //Thread detect-recursion(self):
              [~*attrs] each [v]:
                result << v inspect << comma

            when(recursed):
              return(result + "...)")

            Rubinius Type infect(result, self)
            result shorten(2)!

            result << ")"

        ~pat-def

        ~*(cs collect [c]: define-class(name, c)))

macro(data ~(children: Block)):
  `(Object data: ~children)

macro(data(~x)):
  `(Object data(~x) {})

macro(~root data(~x)):
  `(~root data(~x) {})

macro(data(~parent): ~*children):
  `(Object data(~parent): ~*children)

macro(~root data: ~*children):
  `(do:
      ~*(children collect [c]:
          define-class(root, c))
      nil)

macro(~root data(~parent): ~*children):
  `(do:
      ~(define-class(root, `(~(parent): ~*children)))
      nil)
