use(require("core"))

-- function definition
macro(fn((~(msg & Word))(~*args)): ~*body):
  Atomy Code DefineFunction new(msg text, `(do: ~*body), args)

macro(fn(~(msg & Word)): ~*body):
  Atomy Code DefineFunction new(msg text, `(do: ~*body), [])

macro(fn(~(msg & Word) &~blk): ~*body):
  Atomy Code DefineFunction new(msg text, `(do: ~*body), [], nil, blk)

macro(fn((~(msg & Word))(~*args) &~blk): ~*body):
  Atomy Code DefineFunction new(msg text, `(do: ~*body), args, nil, blk)

-- method definition
macro(def((~(msg & Word))(~*args)): ~*body):
  Atomy Code DefineMethod new(msg text, `(do: ~*body), args)

macro(def((~(msg & Word))!(~*args)): ~*body):
  Atomy Code DefineMethod new((msg text to-s + "!") to-sym, `(do: ~*body), args)

macro(def((~(msg & Word))?(~*args)): ~*body):
  Atomy Code DefineMethod new((msg text to-s + "?") to-sym, `(do: ~*body), args)

macro(def(~(msg & Word)): ~*body):
  Atomy Code DefineMethod new(msg text, `(do: ~*body), [])

macro(def(~(msg & Word) &~blk): ~*body):
  Atomy Code DefineMethod new(msg text, `(do: ~*body), [], nil, blk)

macro(def((~(msg & Word))(~*args) &~blk): ~*body):
  Atomy Code DefineMethod new(msg text, `(do: ~*body), args, nil, blk)

-- helper for adding a module to the constant scope
with-module = Class new:
  def(bytecode(gen, _)):
    gen push-self
    gen add-scope
    gen push-nil

-- module/class opening
macro(~x open: ~*body):
  `(~x module-eval:
      ~(with-module new)
      ~*body)

-- class creation
macro(class: ~*body): `(Object class: ~*body)

macro(~parent class: ~*body):
  `(//Class new(~parent) open:
      ~*body
      self)

-- singleton class opening
macro(singleton: ~*body): `(self singleton: ~*body)

macro(~x singleton: ~*body):
  `(~x singleton-class open: ~*body)

-- module creation
macro(module: ~*body):
  `(//Module new open:
      ~*body
      self)
