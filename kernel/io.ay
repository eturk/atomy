use("core")
use("define")
use("control-flow")

use("dynamic")

OutputPort = dynamic($stdout)
InputPort = dynamic($stdin)
ErrorPort = dynamic($stderr)

puts(x) := x tap: ^OutputPort puts(x)
print(x) := x tap: ^OutputPort print(x)

with-output-to((filename: String), mode = "w") &action :=
  with(OutputPort = File open(filename, mode)):
    action ensuring:
      ^OutputPort close

with-output-to(io) &action :=
  with(OutputPort = io):
    action call

with-error-to((filename: String), mode = "w") &action :=
  with(ErrorPort = File open(filename, mode)):
    action ensuring:
      ^ErrorPort close

with-error-to(io) &action :=
  with(ErrorPort = io):
    action call


read-line(sep = $separator) := do:
  x = ^InputPort gets(sep)

  unless(x):
    error(EndOfFile new(^InputPort))

  x

read-lines(sep = $separator) := do:
  x = ^InputPort readlines(sep)

  unless(x):
    error(EndOfFile new(^InputPort))

  x

read-byte := do:
  x = ^InputPort getc

  unless(x):
    error(EndOfFile new(^InputPort))

  x

read-char := read-byte chr

read(length = nil, buffer = nil) :=
  ^InputPort read(length, buffer)

with-input-from((filename: String), mode = "r") &action :=
  with(InputPort = File open(filename, mode)):
    action ensuring:
      ^InputPort close

with-input-from(io) &action :=
  with(InputPort = io):
    action call
