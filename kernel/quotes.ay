use("core")
use("define")
use("control-flow")

macro(macro-quoter(~n) [~*as]: ~*body):
  names [str, args]:
    strpat = Unquote new [u]: u expression = `(~str: StringLiteral)
    argpat = Unquote new [u]: u expression = `*~args

    raw = as[0]
    when(raw):
      raw =! `(~raw = ~str raw or ~str value)

    flags = as[1]
    when(flags):
      flags =! `(~flags = ~args collect .text)

    val = as[2]
    when(val):
      val =! `(~val = ~str value)

    `(do:
        macro(~n ~strpat):
          ~args = []
          ~*([raw, flags, val] compact)
          ~*body

        macro(~n (~strpat)(~argpat)):
          ~*([raw, flags, val] compact)
          ~*body)


macro-quoter(w) [_, _, v]: v split

macro-quoter(s) [_, _, v]:
  v split collect [s]:
    if(s =~ Regexp new("[\p{Ll}_]", Regexp KCODE_UTF8))
      then: s tr("-", "_") to-sym
      else: s to-sym

macro-quoter(raw) [r]: r
