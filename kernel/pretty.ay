use("core")
use("define")
use("control-flow")
use("cosmetics")
use("particles")
use("range")
use("regexp")
use("array")
use("patterns")
use("interpolation")
use("dynamic")
use("patterns")
use("init")

doc = use("doc")
io = require("io")

Printing = dynamic([])
Multiline = dynamic(false)
Context = dynamic(.top)
Colored = dynamic(false)

ident(n) =
  if(n =~ r"^__")
    then: doc text(n)
    else: doc text(n [0, 1] + n [1 .. -1] tr("_", "-"))

from(*where) &y =
  let(Context = where flatten):
    y call

from(*where)? =
  [^Context, where] match:
    [(a: Array), (b: Array)] ->
      a any? .(is-in b)

    [(a: Array), b] ->
      b is-in a

    [a, (b: Array)] ->
      a is-in b

    [a, b] ->
      a == b

render-sequence([]) = []
render-sequence([x]) = [from(.top, .list): render(x)]
render-sequence(x . xs) = [from(.list): render(x)] + render-sequence(xs)

tree(xs) = align(fill-sep(comma punctuate(xs)))
body(xs) = tree(render-sequence(xs))

vtree(xs) = align(vcat(xs))
vbody(xs) = from(.top): vtree(render(x) for x in xs)

array(xs) =
  enclose(
    lbracket
    rbracket
    align(suggest-sep(comma punctuate(xs))))

colored(color, doc) = do:
  unless(^Colored and ^(io OutputPort) tty?):
    return(doc)

  codes = [
    .black
    .red
    .green
    .yellow
    .blue
    .magenta
    .cyan
    .white
  ]

  raw(i"\e[3#{codes index(color)}m") <> doc <> raw("\e[0m")


unamb(x) := render(x)

render(x) :=
  if(x is-in ^Printing)
    then: text("...")
    else:
      let(Printing = x . ^Printing):
        doc(x)

show(x) :=
  let(Colored = false):
    render(x) flatten render

puts(x) := x tap: ^(io OutputPort) puts(show(x))
print(x) := x tap: ^(io OutputPort) print(show(x))

doc(x) := text(x inspect)

doc(s: String) := colored(.yellow, text(s inspect gsub("\\#", "#")))
doc(x: Numeric) := colored(.blue, text(x inspect))
doc(true) := colored(.green, text("true"))
doc(false) := colored(.red, text("false"))
doc(nil) := colored(.black, text("nil"))
doc(? equal(_)?) := colored(.black, text("_"))
doc(d: Doc) := d
doc(a: Array) := list(render-sequence(a))

doc(p: Particle) := do:
  operator? = p message to-s !~ r"^[a-z_]"

  msg-args =
    if(operator?)
      then:
        text(p message to-s) <+> unamb(p arguments first)
      else:
        ident(p message to-s) <>
          tupled(render-sequence(p arguments))

  partial =
    if(p receiver equal(_)?)
      then:
        if(operator?)
          then: parens(msg-args)
          else: msg-args
      else: parens(unamb(p receiver) <+> msg-args)

  text(".") <> partial

doc(s: Symbol) := do:
  str = s to-s
  if(str =~ r"\[\]=?|[A-Z][a-zA-Z0-9_]*|^[\p{Ll}_][\p{L}_\d\-]*[!?=]?$")
    then: text(".") <> ident(str)
    else: text(".") <> doc(str)

doc(h: Hash) :=
  text("#") <>
    align(
      ( lbrace </>
          tree(
            h to-a collect [k, v]:
              from(.infix):
                unamb(k) <+> text("->") <+> unamb(v))
      ) nest(2) </>
        rbrace)

grouped(x) = from(.top): parens(render(x))

Atomy AST open:
  /unamb(
      x:
        Block
          | ClassVariable
          | Call
          | Constant
          | GlobalVariable
          | (Tree ? @nodes size < 2)
          | InstanceVariable
          | List
          | Literal
          | Primitive
          | Word) :=
    render(x)
  /unamb(c: Compose) :=
    if(from(.infix, .compose)?)
      then: render(c)
      else: grouped(c)
  /unamb(s: Send) :=
    if(from(.infix, .compose)?)
      then: render(s)
      else: grouped(s)
  /unamb(p: Prefix | Postfix | QuasiQuote | Quote | Unquote) :=
    if(from(.postfix, .call)?)
      then: grouped(p)
      else: render(p)
  /unamb(p: Pattern) :=
    unamb(p pattern)
  /unamb(n: Node) :=
    grouped(n)


  braces-block(contents, left = /empty) =
    align(left <+> (lbrace <+> body(contents)) nest(2) <+> rbrace) <||>
      if(left empty?)
        then: align((lbrace <+> vbody(contents)) nest(2) <$> rbrace)
        else: align(left <+> (lbrace <$> vbody(contents)) nest(2) <$> rbrace)

  proc-arg(left, contents) =
    if(from(.top)?)
      then:
        single = colon <+> body(contents)
        multi = colon <$> vbody(contents)

        block =
          if(from(.list)?)
            -- when in a list, prefer the single-line form
            --
            -- e.g.:
            --     macro(foo: bar):
            --       baz
            --
            -- here it's better to have `foo: bar` on one line
            then: single <|> multi

            -- otherwise, prefer the multiline form
            else: single <||> multi

        align((left <> block) nest(2))
      else:
        braces-block(contents, left)


  /doc(b: Infix) :=
    from(.infix):
      ( if(b private)
          then:
            text(b operator) </> unamb(b right)
          else:
            unamb(b left) <+> text(b operator) <\> unamb(b right)
      ) hang(2)

  /doc(b: Block) := do:
    block-arg =
      if(b block)
        then: doc(`&~(b block))
        else: empty

    braces-block(
      b contents
      if(b arguments empty?)
        then: block-arg
        else: list(render-sequence(b arguments)) <+> block-arg)

  /doc(c: Call) :=
    from(.call) { unamb(c name) } <> tupled(render-sequence(c arguments))

  /doc(`(~x [~*ys] { ~*zs })) :=
    proc-arg(
      from(.compose) { unamb(x) } <+>
        list(render-sequence(ys))
      zs)

  /doc(`(~x { ~*ys })) := do:
    proc-arg(
      from(.compose): unamb(x)
      ys)

  /doc(c: Compose) :=
    from(.compose):
      unamb(c left) <+> unamb(c right)

  /doc(c: Constant) := text(c name)

  /doc(l: List) :=
    array(render-sequence(l elements)) <||>
      align(
        lbracket <+>
          vbody(l elements) <$>
          rbracket)

  /doc(l: Literal) := render(l value)

  /doc(p: Postfix) :=
    from(.postfix):
      unamb(p receiver) <> text(p operator)

  /doc(p: Prefix) :=
    from(.prefix):
      text(p operator) <> unamb(p receiver)

  /doc(Primitive with(@value, .self)) := text("self")
  /doc(Primitive with(@value, .true)) := text("true")
  /doc(Primitive with(@value, .false)) := text("false")
  /doc(Primitive with(@value, .nil)) := text("nil")
  /doc(Primitive with(@value, v)) := text(v inspect)

  /doc(q: QuasiQuote) :=
    from(.prefix):
      text("`") <> unamb(q expression)

  /doc(q: Quote) :=
    from(.prefix):
      text("'") <> unamb(q expression)

  /doc(s: ScopedConstant) :=
    from(.compose):
      unamb(s parent) <+> text(s name)

  /doc(s: StringLiteral) := text(s value inspect gsub("\\#", "#"))

  /doc(t: ToplevelConstant) :=
    from(.compose):
      text("_") <+> text(t name)

  /doc(u: Unquote) :=
    from(.prefix):
      text("~") <> unamb(u expression)

  /doc(w: Word) := ident(w text to-s)

  /doc(t: Tree) :=
    if(^Multiline)
      then: vbody(t nodes)
      else: body(t nodes)

  /doc(p: Pattern) := render(p pattern)


Atomy Patterns open:
  /doc(a: And) := render(`(~(a a) & ~(a b)))
  /doc(Any) := text("_")
  /doc(a: Attribute) :=
    render(`(~(a receiver) ~init(Atomy AST Word, text: a name)))
  /doc(b: BlockPass) := render(`&~(b pattern))
  /doc(c: Constant) := render(c constant)
  /doc(d: Default) := render(`(~(d pattern) = ~(d default)))
  /doc(h: HeadTail) := render(`(~(h head) . ~(h tail)))
  /doc(l: List) := array(render-sequence(@patterns))
  /doc(l: Literal) := render(l value)
  /doc(m: Match) := render(m value)
  /doc(n: Named) :=
    if(n pattern is-a(Any)?)
      then: text(n name)
      else: text(n name) <> text(":") <+> render(n pattern)
  /doc(n: NamedClass) :=
    render(`(@@~init(Atomy AST Word, text: n identifier)))
  /doc(n: NamedInstance) :=
    render(`(@~init(Atomy AST Word, text: n identifier)))
  /doc(n: NamedGlobal) :=
    render(`($~init(Atomy AST Word, text: n identifier)))
  /doc(o: Or) :=
    render(`(~(o a) | ~(o b)))
  /doc(p: Particle) :=
    render(
      Particle new(
        p receiver
        p message
        p arguments patterns))
  /doc(p: Predicate) := render(`(~(p pattern) ? ~(p test)))
  /doc(q: QuasiQuote) := render(q quoted)
  /doc(q: Quote) := render(`'~(q expression))
  /doc(s: SingletonClass) := render(s body)
  /doc(s: Splat) := render(`*~(s pattern))
  /doc(w: With) :=
    render(`(~(w pattern) with(~(w expression), ~(w sub-pattern))))

  /unamb(
      x: And | Attribute | Default | HeadTail | Or | Predicate | With) :=
    grouped(x)
  /unamb(n: Named) :=
    if(n pattern is-a(Any)?)
      then: render(n)
      else: grouped(n)
