infix("=", 10, "right")
infix("?", 30, "right")

-- a few macros so we can actually, you know, send messages
define-macro('(~_ ~(? to-word)), 'make-send(node))
define-macro('(~_ (~(? to-word))(~*_)), 'make-send(node))

-- a macro for defining macros!
define-macro(
  '(macro(~pat) ~(body: Block))
  '(DefineMacro bootstrap(node line, pat, body body)))

-- assignment/pattern-matching
macro(~x = ~y): Assign bootstrap(node line, x, y)

-- pseudo-variables
macro(nil): Primitive bootstrap(node line, "nil" to-sym)
macro(self): Primitive bootstrap(node line, "self" to-sym)
macro(true): Primitive bootstrap(node line, "true" to-sym)
macro(false): Primitive bootstrap(node line, "false" to-sym)
macro(_): Primitive bootstrap(node line, "undefined" to-sym)

macro(//~(c: Constant)):
  x = ToplevelConstant new
  x name = c name
  x

macro(/~(c: Constant)):
  x = ScopedConstant new
  x parent = 'Self
  x name = c name
  x

macro(/~(w ? to-word)):
  x = w to-send
  x receiver = 'Self
  x

macro(/~send):
  x = expand(send) to-send
  x receiver = 'Self
  x

macro(~p ~(c: Constant)):
  x = ScopedConstant new
  x parent = p
  x name = c name
  x

-- [x, y] { x + y }
macro([~*args]: ~*body):
  x = Block new
  x contents = body
  x arguments = args
  x

-- [x, y] &z { x + y }
macro([~*args] &~blk { ~*body }):
  x = Block new
  x contents = body
  x arguments = args
  x block = blk
  x

-- &z { x + y }
macro(&~blk { ~*body }):
  x = Block new
  x contents = body
  x block = blk
  x

-- foo { bar }
macro(~(w ? to-word) ~(b: Block)):
  x = w to-send
  x block = b
  x

-- foo(a) { bar }
macro((~(? to-word))(~*_) ~(b: Block)):
  x = node left to-send
  x block = b
  x

-- x foo { bar }
macro(~_ ~(? to-word) ~(b: Block)):
  x = node left to-send
  x block = b
  x

-- x foo(a) { bar }
macro(~_ (~(? to-word))(~*_) ~(b: Block)):
  x = node left to-send
  x block = b
  x

-- collect [x] { x + 1 }
macro(~(meth ? to-word) [~*args]: ~*body):
  x = meth to-send
  b = Block new
  b contents = body
  b arguments = args
  x block = b
  x

-- collect(a) [x] { x + 1 }
macro((~(? to-word))(~*_) [~*args]: ~*body):
  x = node left left to-send
  b = Block new
  b contents = body
  b arguments = args
  x block = b
  x

-- [1, 2, 3] collect [x] { x + 1 }
macro(~_ ~(? to-word) [~*args]: ~*body):
  x = node left left to-send
  b = Block new
  b contents = body
  b arguments = args
  x block = b
  x

-- [1, 2, 3] collect(a) [x] { x + 1 }
macro(~_ (~(? to-word))(~*_) [~*args]: ~*body):
  x = node left left to-send
  b = Block new
  b contents = body
  b arguments = args
  x block = b
  x

-- particle/symbol block-passing shorthand
macro(~x .~y): `(~x &.~y)

-- block-passing
macro(~_ ~(? to-word) &~_):
  node left to-send tap [s]:
    s block = node right

macro((~(? to-word))(~*_) &~_):
  node left to-send tap [x]:
    x block = node right

macro(~_ (~(? to-word))(~*_) &~_):
  node left to-send tap [s]:
    s block = node right

-- foo [bar, ...]
macro(~x [~*ys]): node to-send

-- foo [bar, ...] &baz
macro(~x [~*ys] &~x):
  node left to-send tap [s]:
    s block = node right

-- [bar, ...] &baz
macro([~*_] &~_):
  `(self ~(node left)) to-send tap [s]:
    s private = true
    s block = node right

-- Foo &bar
macro(~(c: Constant) &~_):
  `((~c)()) to-send tap [x]:
    x message-name = c name
    x block = node right

-- Foo(...)
macro((~(c: Constant))(~*args)):
  node to-send tap [x]:
    x message-name = c name

-- Foo(...) &baz
macro((~(c: Constant))(~*args) &~_):
  node left to-send tap [x]:
    x message-name = c name
    x block = node right

-- x Foo(...)
macro(~r (~(c: Constant))(~*args)):
  node right to-send tap [x]:
    x message-name = c name
    x receiver = r
    x private = false

-- x Foo &baz
macro(~r ~(c: Constant) &~_):
  `((~c)()) to-send tap [x]:
    x message-name = c name
    x receiver = r
    x block = node right
    x private = false

-- x Foo(...) &baz
macro(~r (~(c: Constant))(~*args) &~_):
  `((~c)(~*args)) to-send tap [x]:
    x message-name = c name
    x receiver = r
    x block = node right
    x private = false

macro(_LINE): node line
macro(_FILE): ActivePath new

macro(&~x):
  BlockPass new [b]: b body = x

macro(*~x):
  Splat new [s]: s value = x

macro(@~(x: ? to-word)):
  InstanceVariable new [g]:
    g identifier = x to-word text

macro(@@~(x: ? to-word)):
  ClassVariable new [g]:
    g identifier = x to-word text

macro($~(x: StringLiteral)):
  GlobalVariable new [g]:
    g identifier = x value

macro($~(x: Constant)):
  GlobalVariable new [g]:
    g identifier = x name

macro($~(x: ? to-word)):
  GlobalVariable new [g]:
    g identifier = x to-word text

macro($0):
  GlobalVariable new [g]:
    g identifier = "0" to-sym

macro($exception):
  GlobalVariable new [g]:
    g identifier = "!" to-sym

macro($path):
  GlobalVariable new [g]:
    g identifier = ":" to-sym

macro($separator):
  GlobalVariable new [g]:
    g identifier = "/" to-sym

macro(.~(x: Constant)):
  Literal new [l]:
    l value = x name

macro(.~(x: ? to-word)):
  Literal new [l]:
    l value = x to-word text

macro(.~(x: StringLiteral)):
  Literal new [l]:
    l value = x value to-sym

macro(.[]): ."[]"
macro(.[]=): ."[]="

-- bar?
macro((~Word)?): node to-word

-- x bar?
macro(~x (~Word)?):
  node dup tap [c]:
    c right = node right to-word


-- bar(...)?
macro((~Word)(~*args)?):
  node receiver copy tap [r]:
    r name text = (r name text to-s + "?") to-sym

-- foo bar(...)?
macro(~x (~Word)(~*args)?):
  node copy tap [c]:
    c right =
      c right receiver tap [r]:
        r name text = (r name text to-s + "?") to-sym

-- bar!
macro((~Word)!): node to-word

-- x bar!
macro(~x (~Word)!):
  node dup tap [c]:
    c right = c right to-word


-- bar(...)!
macro((~Word)(~*args)!):
  node receiver copy tap [r]:
    r name text = (r name text to-s + "!") to-sym

-- foo bar(...)!
macro(~x (~Word)(~*args)!):
  node copy tap [c]:
    c right =
      c right receiver tap [r]:
        r name text = (r name text to-s + "!") to-sym


use("operators")
